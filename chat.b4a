Build1=Default,b4x.chatbot.df
File1=1.bal
File10=roboto-regular.ttf
File11=settings.png
File2=Acme-Regular.ttf
File3=clvTextoAssistente.bal
File4=clvTextoUsuario.bal
File5=eMensagem.png
File6=eVoz.png
File7=menu.png
File8=ponta.png
File9=pontaCinza.png
FileGroup1=Default Group
FileGroup10=Default Group
FileGroup11=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
FileGroup6=Default Group
FileGroup7=Default Group
FileGroup8=Default Group
FileGroup9=Default Group
Group=Default Group
Library1=core
Library2=xcustomlistview
Library3=xui
Library4=javaobject
Library5=stringutils
Library6=phone
Library7=ime
ManifestCode=AddManifestText(~\n~<uses-sdk android:minSdkVersion="5" android:targetSdkVersion="28"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true"~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~CreateResourceFromFile(Macro, Themes.LightTheme)~\n~~\n~SetActivityAttribute(main, android:windowSoftInputMode, adjustResize|stateHidden)
Module1=geral
Module2=Starter
NumberOfFiles=11
NumberOfLibraries=7
NumberOfModules=2
Version=9.801
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: CHATBOT
	#VersionCode: 1
	#VersionName: 1.0
	#SupportedOrientations: portrait
	#CanInstallToExternalStorage: True
#End Region

#Region  Activity Attributes 
	#FullScreen: False
	#IncludeTitle: False
#End Region

Sub Process_Globals
	Type mensagemTexto (mensagem As String, assistente As Boolean)
	Private su As StringUtils
End Sub

Sub Globals
	
	'TECLADO
	Private ime As IME
	Private heightTeclado As Int
	Private tamanhoMaximo As Int = 0
	

	'CHAT
	Private clvMensagens As CustomListView
	Private edEscrever As EditText
	Private icEscrever As ImageView
	Private pEscrever As Panel
	Private pTopMenu As Panel
	Private lbTituloTopMenu As Label
	Private icMenuTopMenu As ImageView
	Private icConfigTopMenu As ImageView
	
	
	'CLV TEXTO ASSISTENTE
	Private lbTextoAssistente As Label
	Private pFundoFala As Panel
	Private imgPontaAssistente As ImageView
	
	'CLV TEXTO USUÁRIO
	Private lbTextoUsuario As Label
	Private pFundoFalaUsuario As Panel
	Private imgPontaUsuario As ImageView
	
	
	
	
End Sub

Sub Activity_Create(FirstTime As Boolean)
	geral.Set_StatusBarColor(Colors.RGB(89,89,89))
	Activity.LoadLayout("1")
	
	ime.Initialize("ime")
	ime.AddHeightChangedEvent
	
	
	'TOP MENU
	Private csTitulo As CSBuilder
	csTitulo.Initialize
	csTitulo.Color(Colors.White).Append("B4X CHAT").Color(Colors.RGB(255,113,62)).Append("BOT").PopAll
	lbTituloTopMenu.Text = csTitulo
	icMenuTopMenu.SetBackgroundImage(LoadBitmapResize(File.DirAssets, "menu.png", icMenuTopMenu.Width, icMenuTopMenu.Height, True)).Gravity = Gravity.CENTER
	icConfigTopMenu.SetBackgroundImage(LoadBitmapResize(File.DirAssets, "settings.png", icConfigTopMenu.Width, icConfigTopMenu.Height, True)).Gravity = Gravity.CENTER
	


	Private cc As ColorDrawable
	cc.Initialize2(Colors.RGB(250,250,250),10,2,Colors.LightGray)
	pEscrever.Background = cc
	edEscrever.Background = Null
	geral.setPadding(edEscrever,0,0,0,0) 'REMOVE PADDING DO EDITTEXT
	
	icEscrever.SetBackgroundImage(LoadBitmapResize(File.DirAssets, "eVoz.png", icEscrever.Width, icEscrever.Height, True)).Gravity = Gravity.CENTER
	icEscrever.Tag = "voz"


	'CHAMA A FUNÇÃO DE AJUSTAR TAMANHO DO TECLADO
	IME_HeightChanged(100%y,0)
	tamanhoMaximo = su.MeasureMultilineTextHeight(edEscrever,"teste de tamanho!") * 6 '6 LINHAS O EDITTEXT VAI AUMENTAR, DEPOIS DISSO APARECER O SCROLL
	

	Escreve_Bot("Hey! How are you?")
	
	
End Sub

Sub Activity_Resume
	AjustaTamanho_Clv
End Sub

Sub Activity_Pause (UserClosed As Boolean)

End Sub


Sub clvMensagens_ItemClick (Index As Int, Value As Object)
	
End Sub

Sub clvMensagens_VisibleRangeChanged (FirstIndex As Int, LastIndex As Int)
	Dim ExtraSize As Int = 2
	For i = 0 To clvMensagens.Size - 1
		Dim p As Panel = clvMensagens.GetPanel(i)
		If i > FirstIndex - ExtraSize And i < LastIndex + ExtraSize Then
			If p.NumberOfViews = 0 Then
				
				Dim m As mensagemTexto = clvMensagens.GetValue(i)
				
				If m.assistente Then
		
					p.LoadLayout("clvTextoAssistente")
					lbTextoAssistente.Text = m.mensagem	
	
					imgPontaAssistente.Height = 3%y
					imgPontaAssistente.Top = 0
					imgPontaAssistente.SetBackgroundImage(LoadBitmapResize(File.DirAssets, "ponta.png", imgPontaAssistente.Width, imgPontaAssistente.Height, False)).Gravity = Gravity.CENTER
					
					'AJUSTA VERTICAL
					Private margemTop As Int = 1%y : Private margemBottom As Int = 1%y
					lbTextoAssistente.Height = geral.Tamanho_TextoVertical(lbTextoAssistente,lbTextoAssistente.Text)
					lbTextoAssistente.Top = 0%y + margemTop
					
					'AJUSTA HORIZONTAL
					If geral.Tamanho_TextoHorizontal(lbTextoAssistente,lbTextoAssistente.Text) < 82%x Then
						lbTextoAssistente.Width = geral.Tamanho_TextoHorizontal(lbTextoAssistente,lbTextoAssistente.Text)
						lbTextoAssistente.SingleLine = True
						pFundoFala.Width = lbTextoAssistente.Width +4%x
					End If
					
					pFundoFala.Height = lbTextoAssistente.Height + margemTop + margemBottom
					clvMensagens.ResizeItem(i,pFundoFala.Height)
				
				Else
					
					p.LoadLayout("clvTextoUsuario")
					lbTextoUsuario.Text = m.mensagem
					
					imgPontaUsuario.Height = 3%y
					imgPontaUsuario.Top = 0
					imgPontaUsuario.SetBackgroundImage(LoadBitmapResize(File.DirAssets, "pontaCinza.png", imgPontaUsuario.Width, imgPontaUsuario.Height, False)).Gravity = Gravity.CENTER
					
					'AJUSTA VERTICAL
					Private margemTop As Int = 1%y : Private margemBottom As Int = 1%y
					lbTextoUsuario.Height = geral.Tamanho_TextoVertical(lbTextoUsuario,m.mensagem)
					lbTextoUsuario.Top = 0%y + margemTop
					
					'AJUSTA HORIZONTAL
					If geral.Tamanho_TextoHorizontal(lbTextoUsuario,lbTextoUsuario.Text) < 82%x Then
						lbTextoUsuario.Width = geral.Tamanho_TextoHorizontal(lbTextoUsuario,lbTextoUsuario.Text)
						lbTextoUsuario.SingleLine = True
						pFundoFalaUsuario.Width = lbTextoUsuario.Width +4%x
						pFundoFalaUsuario.Left = 100%x - pFundoFalaUsuario.Width - 4%x
					End If
	
					pFundoFalaUsuario.Height = lbTextoUsuario.Height + margemTop + margemBottom
					clvMensagens.ResizeItem(i,pFundoFalaUsuario.Height)
			
				End If
				
				
				
				
				
				
			End If
		Else
			If p.NumberOfViews > 0 Then
				p.RemoveAllViews
			End If
		End If
	Next
	
	
	
End Sub










Sub edEscrever_TextChanged (Old As String, New As String)
	
	'ICONE VOZ OU TEXTO
	If New.Length > 0 Then 
		icEscrever.SetBackgroundImage(LoadBitmapResize(File.DirAssets, "eMensagem.png", icEscrever.Width, icEscrever.Height, True)).Gravity = Gravity.CENTER
		icEscrever.Tag = "texto"
	Else
		icEscrever.SetBackgroundImage(LoadBitmapResize(File.DirAssets, "eVoz.png", icEscrever.Width, icEscrever.Height, True)).Gravity = Gravity.CENTER
		icEscrever.Tag = "voz"
	End If
	
	
	Private i As Int = su.MeasureMultilineTextHeight(edEscrever,New)
	If i > tamanhoMaximo Then Return 'CHEGOU NO LIMITE DE TAMANHO 
	
	If i > 7%y Then 'ESTÁ PEQUENO, VAMOS AUMENTAR ATÉ O LIMITE
		pEscrever.Height = i
		edEscrever.Height = i
		pEscrever.Top = heightTeclado - pEscrever.Height - 1%y
		AjustaTamanho_Clv
	End If
	
End Sub



Sub IME_HeightChanged(NewHeight As Int, OldHeight As Int)
	heightTeclado = NewHeight
	pEscrever.SetLayout(pEscrever.Left, heightTeclado - pEscrever.Height - 1%y, pEscrever.Width, pEscrever.Height)
	icEscrever.SetLayout(icEscrever.Left, heightTeclado - icEscrever.Height - 1%y, icEscrever.Width, icEscrever.Height)
	AjustaTamanho_Clv
End Sub



Sub AjustaTamanho_Clv
	clvMensagens.AsView.Top = 0 + pTopMenu.Height
	clvMensagens.AsView.Height = pEscrever.Top - pTopMenu.Height - 1%y
	clvMensagens.Base_Resize(clvMensagens.AsView.Width,clvMensagens.AsView.Height)
	Sleep(0) 'PARA TER CERTEZA QUE AJUSTOU O TAMANHO, ANTES DE DESCER O SCROLL (IMPORTANTE O SLEEP AQUI!)
	If clvMensagens.Size > 0 Then clvMensagens.JumpToItem(clvMensagens.Size - 1)
End Sub





Sub icEscrever_Click
	If icEscrever.Tag = "texto" Then
		Private sTexto As String = edEscrever.Text.Trim
		Esconde_Teclado
		Escreve_Usuario(sTexto)
		Conversa_Bot
	Else
		Log("fala")
	End If
End Sub











Sub Escreve_Usuario(mensagem As String) 'LADO DIREITO (CINZA)
	Dim m As mensagemTexto
	m.Initialize
	m.mensagem = mensagem
	m.assistente = False
	Dim p As Panel
	p.Initialize("p")
	p.SetLayoutAnimated(0, 0, 0, clvMensagens.AsView.Width, 15%y)
	clvMensagens.Add(p, m)
	AjustaTamanho_Clv
End Sub


Sub Escreve_Bot(mensagem As String) 'LADO ESQUERDO (AZUL)
	Dim m As mensagemTexto
	m.Initialize
	m.mensagem = mensagem
	m.assistente = True
	Dim p As Panel
	p.Initialize("p")
	p.SetLayoutAnimated(0, 0, 0, clvMensagens.AsView.Width, 15%y)
	clvMensagens.Add(p, m)
	AjustaTamanho_Clv
End Sub

Sub Esconde_Teclado
	edEscrever.Text = ""
	pEscrever.Height = 7%y
	edEscrever.Height = 7%y
	ime.HideKeyboard
End Sub






'RESPOSTAS DO BOT
Sub Conversa_Bot
	If clvMensagens.Size = 2 Then
		Sleep(1200)
		Escreve_Bot("What a good guy! What do you want to do today??")
	else if clvMensagens.Size = 4 Then
		Sleep(1200)
		Escreve_Bot("Sure? 🤔")
	else if clvMensagens.Size = 6 Then
		Sleep(1200)
		Escreve_Bot("So let's go! 🍺🍺🍺🍻🍻")
	else if clvMensagens.Size = 8 Then
		Sleep(1200)
		Escreve_Bot("what the fuck???")
	else if clvMensagens.Size = 10 Then
		Sleep(1200)
		Escreve_Bot("Subscribe to: SerBermz!")
		
	End If
	
	
End Sub




